
// Create a jar with javadoc
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

// Create a jar with soruces
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// hook all the artifacts under artifacts task
artifacts {
    archives javadocJar, sourcesJar
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")


// Signing task
if (isReleaseVersion) {
    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }
}

uploadArchives {

    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            String ossPassword = credentials.ossrhPassword

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossPassword)
            }

            pom.project {

                name 'blox4j-lib'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'An Api Layer For Aion Blockchain'
                url 'https://github.com/bloxbean/bloxql'

                scm {
                    connection 'scm:git:git@github.com:bloxbean/blox4j.git'
                    developerConnection 'scm:git:git@github.com:bloxbean/blox4j.git'
                    url 'https://github.com/bloxbean/bloxql'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'satran004'
                        name 'Satya'
                        email 'satran004@gmail.com'
                    }
                }
            }
        }
    }
}
